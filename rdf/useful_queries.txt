## Useful queries 
Try yourself the queries in the SPARQL endpoint http://w3id.org/esw/sparql

1. Get the best workflow for topics of which there is a ground truth
The query returns a list of 4-tuples (workflow IRI, topic IRI, score, ground truth IRI)

Query text:

PREFIX esw: <http://w3id.org/esw/ontology#>

SELECT ?w ?t ?fs ?gt WHERE{
    
    {
        SELECT ?topic ?macro (MAX(?fscore) AS ?max_score) WHERE
        {
            {
                select ?work (AVG(?score) AS ?fscore) WHERE{
                    ?work a esw:SearchWorkflow;
                          esw:hasPart ?part.
                    ?part esw:fscore ?score.
                }
                GROUP BY ?work 
            }
            FILTER(?fscore > 0.1).
            ?work esw:implements ?topic.
            ?topic esw:description ?macro.
        }
        GROUP BY ?topic ?macro

        }
    
    {
        select ?w ?t (AVG(?s) AS ?fs) WHERE{
            ?w a esw:SearchWorkflow;
                esw:hasPart ?p.
                ?p esw:fscore ?s.
            ?w esw:implements ?t.
            
        }
        GROUP BY ?w ?t 
    }
    FILTER(?fs = ?max_score).
    FILTER(?t = ?topic).
    ?t esw:hasGroundTruth ?gt;
        esw:description ?m.
}

2. Given a specific topic, return the workflows on that topic with their average fscore 
   and the total number of queries. (For the example we use the topic 
   History Workflow Series (Cultural Movements explorative search) 
   with IRI http://w3id.org/esw/resource/TOPICd1c898149d)
The query returns a list of 3-tuples (workflow IRI, score, numberOfQueries)

Query text:

PREFIX esw: <http://w3id.org/esw/ontology#>
PREFIX eswr: <http://w3id.org/esw/resource/>

SELECT DISTINCT ?work (AVG(?fscore) AS ?fs) (SUM(?numQueries) AS ?totQueries) where{
    ?work esw:implements eswr:TOPICd1c898149d;
    	  esw:hasPart ?part.
   	?part esw:fscore ?fscore;
          esw:numberOfQueries ?numQueries.
}GROUP BY ?work

3. Return the tasks with more queries
   The query returns a list 4-tuples (task IRI, task label, numberOfQueries, numberOfWorkflows)

Query text:

PREFIX esw: <http://w3id.org/esw/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?task ?label (SUM(?numQueries) AS ?totQueries) (COUNT(DISTINCT ?job) AS ?works) where{
    ?job a esw:SearchJob;
         esw:performs ?task;
         esw:numberOfQueries ?numQueries.
     ?task rdfs:label ?label.
}GROUP BY ?task ?label
ORDER BY DESC(?totQueries)
LIMIT 10